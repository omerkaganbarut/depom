#include <Arduino.h>
#include "config.h"
#include "stepmotorenkoderiokuma.h"

const int toplamPulse = 2000;
int a0Veri[toplamPulse + 1];
int a0Min = 400;
int a0Max = 800;
long encMin = 0;
long encMax = 450250;
const int tolerans = 100;

unsigned long zTakipUs = 0;
unsigned long bigOncekiMs = 0;
unsigned long bigHighBaslangic = 0;

bool kayitModu = false;
bool oynatmaModu = false;
bool zIlkHazir = false;
bool bigPulseHigh = false;

int pulseIndex = 0;

StepMotorEncoder zEncoder(ENC1_A_PIN, ENC1_B_PIN);

void zMotorStepAt() {
  digitalWrite(STEP1_PIN, HIGH);
  zTakipUs = micros();
  // LOW sinyali sonraki loop()'ta atılacak, delay kullanmadan
}

void setup() {
  pinMode(STEP1_PIN, OUTPUT);
  pinMode(DIR1_PIN, OUTPUT);
  pinMode(ENA1_PIN, OUTPUT);

  pinMode(STEP3_PIN, OUTPUT);
  pinMode(DIR3_PIN, OUTPUT);
  pinMode(ENA3_PIN, OUTPUT);

  pinMode(PUSH1_PIN, INPUT_PULLUP);
  pinMode(PUSH2_PIN, INPUT_PULLUP);

  zEncoder.begin();

  Serial.begin(9600);
}

void loop() {
  unsigned long simdiMs = millis();
  unsigned long simdiUs = micros();

  // Serial komut kontrolü
  if (Serial.available()) {
    String komut = Serial.readStringUntil('\n');
    komut.trim();

    if (komut == "k" || komut == "kayit") {
      kayitModu = true;
      oynatmaModu = false;
      pulseIndex = 0;
      a0Veri[0] = analogRead(OPKON_PIN);
      a0Min = a0Veri[0];
      a0Max = a0Veri[0];
      bigOncekiMs = millis();
      Serial.println(">> KAYIT MODU BASLADI");
    }

    if (komut == "o" || komut == "oynat") {
      kayitModu = false;
      oynatmaModu = true;
      pulseIndex = 0;
      zIlkHazir = false;
      bigOncekiMs = millis();
      Serial.println(">> OYNATMA MODU BASLADI");
    }
  }

  // Z step pinini LOW yap (10 µs sonra)
  if (digitalRead(STEP1_PIN) == HIGH && simdiUs - zTakipUs >= 10) {
    digitalWrite(STEP1_PIN, LOW);
  }

  // Big step LOW yap ve index artır
  if (digitalRead(STEP3_PIN) == HIGH && simdiMs - bigHighBaslangic >= 1) {
    digitalWrite(STEP3_PIN, LOW);
    bigPulseHigh = false;
    bigOncekiMs = simdiMs;
    pulseIndex++;
  }

  // ---------- KAYIT MODU ----------
  if (kayitModu && pulseIndex < toplamPulse) {
    if (!bigPulseHigh && simdiMs - bigOncekiMs >= 9) {
      digitalWrite(STEP3_PIN, HIGH);
      bigHighBaslangic = simdiMs;
      bigPulseHigh = true;
    }

    if (!bigPulseHigh) {
      pulseIndex++;
      int val = analogRead(OPKON_PIN);
      a0Veri[pulseIndex] = val;
      if (val < a0Min) a0Min = val;
      if (val > a0Max) a0Max = val;
    }

    if (pulseIndex >= toplamPulse) {
      kayitModu = false;
      Serial.println(">> KAYIT TAMAMLANDI");
    }

    return;
  }

  // ---------- OYNATMA MODU ----------
  if (oynatmaModu && !zIlkHazir) {
    long hedefEnc = map(a0Veri[0], a0Min, a0Max, encMin, encMax);
    long mevcutEnc = zEncoder.getPosition();

    if (abs(hedefEnc - mevcutEnc) <= tolerans) {
      zIlkHazir = true;
      Serial.println(">> Z BASLANGIC KONUMUNA GELDI");
      return;
    }

    bool zDir = (hedefEnc > mevcutEnc);
    digitalWrite(DIR1_PIN, zDir ? HIGH : LOW);
    if (simdiUs - zTakipUs >= 100) {
      zMotorStepAt();
    }

    return;
  }

  if (oynatmaModu && zIlkHazir && pulseIndex < toplamPulse) {
    int a0Deger = a0Veri[pulseIndex];
    int hedefFreq = map(a0Deger, a0Min, a0Max, 100, 200);
    int sureMs = 1000 / hedefFreq;
    int lowMs = sureMs - 1;

    if (!bigPulseHigh && simdiMs - bigOncekiMs >= lowMs) {
      digitalWrite(STEP3_PIN, HIGH);
      bigHighBaslangic = simdiMs;
      bigPulseHigh = true;
    }

    long hedefEnc = map(a0Deger, a0Min, a0Max, encMin, encMax);
    long mevcutEnc = zEncoder.getPosition();

    if (abs(hedefEnc - mevcutEnc) > tolerans) {
      bool zDir = (hedefEnc > mevcutEnc);
      digitalWrite(DIR1_PIN, zDir ? HIGH : LOW);
      if (simdiUs - zTakipUs >= 100) {
        zMotorStepAt();
      }
    }
  }

  if (pulseIndex >= toplamPulse && oynatmaModu) {
    oynatmaModu = false;
    Serial.println(">> OYUN BITTI");
  }
}
