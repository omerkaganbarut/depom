#include <Arduino.h>
#include "config.h"
#include "stepmotorenkoderiokuma.h"

// === Motor Seçimi ===
// Sadece test etmek istediğin motorun satırını aktif bırak!

// StepMotorEncoder testEncoder(ENC3_A_PIN, ENC3_B_PIN);  // BIG MOTOR
// #define STEP_PIN STEP3_PIN
// #define DIR_PIN  DIR3_PIN
// #define ENA_PIN  ENA3_PIN

// StepMotorEncoder testEncoder(ENC1_A_PIN, ENC1_B_PIN);  // Z MOTORU
// #define STEP_PIN STEP1_PIN
// #define DIR_PIN  DIR1_PIN
// #define ENA_PIN  ENA1_PIN

StepMotorEncoder testEncoder(ENC2_A_PIN, ENC2_B_PIN);  // X MOTORU
#define STEP_PIN STEP2_PIN
#define DIR_PIN  DIR2_PIN
#define ENA_PIN  ENA2_PIN

// === Pulse parametreleri ===
const int toplamPulse = 10000;
int atilanPulse = 0;

bool stepHigh = false;
unsigned long oncekiUs = 0;

const int pulseFreq = 200;                   // Hz
const int pulseSureUs = 1000000 / pulseFreq; // HIGH + LOW toplam süresi
const int highSureUs = 20;                   // HIGH süresi

// === Seri yazım zamanlayıcısı ===
unsigned long encoderPrintMs = 0;
const unsigned long printAralikMs = 200;

void setup() {
  pinMode(STEP_PIN, OUTPUT);
  pinMode(DIR_PIN, OUTPUT);
  pinMode(ENA_PIN, OUTPUT);

  digitalWrite(DIR_PIN, HIGH);  // sağa dön
  digitalWrite(ENA_PIN, LOW);   // motor aktif (LOW: aktif, HIGH: devre dışı)

  testEncoder.begin();

  Serial.begin(9600);
  Serial.println(">> GENEL MOTOR + ENKODER TESTİ BAŞLADI");
}

void loop() {
  unsigned long simdiUs = micros();
  unsigned long simdiMs = millis();

  // === PULSE ÜRETİMİ ===
  if (atilanPulse < toplamPulse) {
    if (!stepHigh && (simdiUs - oncekiUs >= (pulseSureUs - highSureUs))) {
      digitalWrite(STEP_PIN, HIGH);
      stepHigh = true;
      oncekiUs = simdiUs;
    }

    if (stepHigh && (simdiUs - oncekiUs >= highSureUs)) {
      digitalWrite(STEP_PIN, LOW);
      stepHigh = false;
      oncekiUs = simdiUs;
      atilanPulse++;

      if (atilanPulse >= toplamPulse) {
        Serial.println(">> TAMAMLANDI");
      }
    }
  }

  // === ENCODER DEĞERİNİ YAZ ===
  if (simdiMs - encoderPrintMs >= printAralikMs) {
    encoderPrintMs = simdiMs;
    long pozisyon = testEncoder.getPosition();
    Serial.print("ENKODER: ");
    Serial.println(pozisyon);
  }
}
